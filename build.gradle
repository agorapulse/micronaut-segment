plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "groovy"
    id "maven"
    id "jacoco"
    id "checkstyle"
    id "codenarc"
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

version "0.1.0"
group "com.agorapulse"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.2.0'
    }
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"

    compile "io.micronaut:micronaut-runtime-groovy"
    compile "io.micronaut:micronaut-validation"
    compile "com.segment.analytics.java:analytics:2.1.1"

    compileOnly "io.micronaut:micronaut-inject-groovy"

    runtime "ch.qos.logback:logback-classic:1.2.3"

    testCompile("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "io.micronaut.test:micronaut-test-spock:1.1.0"
    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile 'net.bytebuddy:byte-buddy:1.10.1'
    testCompile 'org.objenesis:objenesis:3.0.1'
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

checkstyle {
    toolVersion = '8.23'
}

codenarc {
    toolVersion = '1.4'
}

// publishing
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['groovyMaven']
    publish = true

    final String projectName = project.name
    final String projectDescription = project.description

    pkg {
        websiteUrl = 'https://github.com/agorapulse/micronaut-segmen'
        issueTrackerUrl = 'https://github.com/agorapulse/micronaut-segment/issues'
        vcsUrl = 'https://github.com/agorapulse/micronaut-segment.git'

        repo = 'libs' // or your repo name
        userOrg = 'agorapulse'
        name = projectName       // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    // dryRun = true // whether to run this as dry-run, without deploying
}
